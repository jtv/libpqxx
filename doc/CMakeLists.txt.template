find_program(HAVE_DOXYGEN doxygen)

if(NOT HAVE_DOXYGEN)
    message(FATAL_ERROR "*****************************************************
Doxygen not found.
Install it, or configure with -DBUILD_DOC=OFF
*****************************************************")
endif()

find_program(HAVE_XMLTO xmlto)

if(NOT HAVE_XMLTO)
    message(FATAL_ERROR "*****************************************************
xmlto not found.
Install it, or configure with -DBUILD_DOC=OFF
*****************************************************")
endif()

set(PQXXVERSION "${CMAKE_PROJECT_VERSION}")
set(top_srcdir "${PROJECT_SOURCE_DIR}")
set(PQXX_ABI "${PROJECT_VERSION_MAJOR}.${PROJECT_VERSION_MINOR}")
set(PQXX_MAJOR "${PROJECT_VERSION_MAJOR}")
set(PQXX_MINOR "${PROJECT_VERSION_MINOR}")

find_program(HAVE_DOT dot)
if(HAVE_DOT)
    set(HAVE_DOT YES)
else()
    set(HAVE_DOT NO)
endif()

configure_file(Doxyfile.in Doxyfile)

if(HAVE_DOXYGEN)
    set(DOXYGEN_SOURCES
###MAKTEMPLATE:FOREACH include/pqxx/*.hxx
        "${PROJECT_SOURCE_DIR}/include/pqxx/###BASENAME###.hxx"
###MAKTEMPLATE:ENDFOREACH
###MAKTEMPLATE:FOREACH include/pqxx/doc/*.md
        "${PROJECT_SOURCE_DIR}/###FILENAME###"
###MAKTEMPLATE:ENDFOREACH
###MAKTEMPLATE:FOREACH src/*.cxx
        "${PROJECT_SOURCE_DIR}/###FILENAME###"
###MAKTEMPLATE:ENDFOREACH
    )
    set(DOXYGEN_STAMP_FILE "${CMAKE_CURRENT_BINARY_DIR}/doxygen.stamp")
    add_custom_command(OUTPUT ${DOXYGEN_STAMP_FILE}
        COMMAND ${CMAKE_COMMAND} -E make_directory ${CMAKE_CURRENT_BINARY_DIR}/html/Reference
        COMMAND doxygen Doxyfile
        COMMAND ${CMAKE_COMMAND} -E touch ${DOXYGEN_STAMP_FILE}
        DEPENDS ${CMAKE_CURRENT_BINARY_DIR}/Doxyfile ${DOXYGEN_SOURCES}
        COMMENT "Generate API documentation"
        VERBATIM
    )
    add_custom_target(doxygen ALL
        DEPENDS ${DOXYGEN_STAMP_FILE}
        SOURCES ${DOXYGEN_SOURCES}
    )
    install(
        DIRECTORY ${CMAKE_CURRENT_BINARY_DIR}/html/Reference
        DESTINATION ${CMAKE_INSTALL_DOCDIR}/html
    )
endif()

if(HAVE_XMLTO)
    set(XMLTO_SOURCES
        "${CMAKE_CURRENT_SOURCE_DIR}/libpqxx.xml"
    )
    set(XMLTO_STAMP_FILE "${CMAKE_CURRENT_BINARY_DIR}/xmlto.stamp")
    add_custom_command(OUTPUT ${XMLTO_STAMP_FILE}
        COMMAND ${CMAKE_COMMAND} -E make_directory ${CMAKE_CURRENT_BINARY_DIR}/html/Tutorial
        COMMAND xmlto -o html/Tutorial xhtml "${CMAKE_CURRENT_SOURCE_DIR}/libpqxx.xml"
        COMMAND ${CMAKE_COMMAND} -E touch ${XMLTO_STAMP_FILE}
        DEPENDS ${XMLTO_SOURCES}
        COMMENT "Generate tutorial"
        VERBATIM
    )
    add_custom_target(tutorial ALL
        DEPENDS ${XMLTO_STAMP_FILE}
        SOURCES ${XMLTO_SOURCES}
    )
    install(
        DIRECTORY ${CMAKE_CURRENT_BINARY_DIR}/html/Tutorial
        DESTINATION ${CMAKE_INSTALL_DOCDIR}/html
    )
endif()
